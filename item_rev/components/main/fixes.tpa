// Item type identifier
INCLUDE ~item_rev/lib/identify_fix.tpa~
INCLUDE ~item_rev/lib/identify_item.tpa~


//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\\\
////\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\

/* ---------------- EE Fixes ---------------- */

ACTION_IF (GAME_IS ~bgee bg2ee eet~ AND NOT GAME_IS ~tutu tutu_totsc bg2 tob bgt~) BEGIN

// Fix Shaman usabilities
COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
  PATCH_IF (~%SOURCE_SIZE%~ > ~0x71~) BEGIN
    READ_SHORT 0x1c type
    READ_BYTE 0x31 prof
    PATCH_IF (%prof% = 92) OR (%prof% = 105) OR (%type% = 5) BEGIN // axes or shortbows or arrows
      WRITE_BYTE 0x21 (THIS BAND 0b10111111)
      LPF DELETE_EFFECT INT_VAR silent = 1 match_opcode  = 319 match_target = 1 match_power = 0 match_parameter2 = 5 match_parameter1 = 11 match_timing = 2 END
      LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 319 target = 1 power = 0 parameter2 = 5 parameter1 = 11 timing = 2 END
    END
  END
BUT_ONLY

// Add two-handed flag for shapeshifts/polymorphs
ACTION_IF ee_two_handed_shapeshifts=1 BEGIN
  ACTION_FOR_EACH resource IN
    ~dvjelly~
    ~dvrat~
    ~dvtroll~
    ~dvwere~
  BEGIN
    ACTION_IF (FILE_EXISTS_IN_GAME ~%resource%.itm~) BEGIN
      COPY_EXISTING ~%resource%.itm~ ~override\%resource%.itm~
      PATCH_IF (SOURCE_SIZE > 0x71) BEGIN
        READ_BYTE 0x18 flags
        WRITE_BYTE 0x18 (%flags% BOR 0b00000010) // Add two-handed flag
      END
    END
  END
END

// EE Spell Protections fixes

ACTION_IF (FILE_EXISTS_IN_GAME ~amul25.spl~) BEGIN // Amulet of Spell Warding's Spell Deflection
COPY_EXISTING ~amul25.spl~ ~override~
  PATCH_IF (SOURCE_SIZE > 0x71) BEGIN // protects against invalid files
  SET opcode = 201            // Set opcode
SET target = 1            // target type
SET timing = 0            // timing type
SET power = 5            // power
SET resist_dispel = 2    // dispel/resitance
SET duration = 60        // duration
SET probability1 = 100    // probability 1
SET probability2 = 0    // probability 2
SPRINT "resource" "amul25b"         // resource
SET dicenumber = 0        // dice number
SET dicesize = 0        // dice size
SET savingthrow = 0   // saving throw
SET savebonus = 0        // saving throw bonus
SET parameter1 = 9 // param1
SET parameter2 = 1 // param2
SET header = 1
LAUNCH_PATCH_MACRO ADD_SPELL_EFFECT
END
END

ACTION_IF (FILE_EXISTS_IN_GAME ~rods01.spl~) BEGIN // Rod of Absorption's Spell Absorption
COPY_EXISTING ~rods01.spl~ ~override~
  PATCH_IF (SOURCE_SIZE > 0x71) BEGIN // protects against invalid files
  SET opcode = 201            // Set opcode
SET target = 1            // target type
SET timing = 0            // timing type
SET power = 5            // power
SET resist_dispel = 2    // dispel/resitance
SET duration = 0        // duration
SET probability1 = 100    // probability 1
SET probability2 = 0    // probability 2
SPRINT "resource" "rods01b"         // resource
SET dicenumber = 0        // dice number
SET dicesize = 0        // dice size
SET savingthrow = 0   // saving throw
SET savebonus = 0        // saving throw bonus
SET parameter1 = 9 // param1
SET parameter2 = 1 // param2
SET header = 1
LAUNCH_PATCH_MACRO ADD_SPELL_EFFECT
END
END

ACTION_IF (FILE_EXISTS_IN_GAME ~staf11.spl~) BEGIN // Staff of the Magi's Spell Trap
COPY_EXISTING ~staf11.spl~ ~override~
  PATCH_IF (SOURCE_SIZE > 0x71) BEGIN // protects against invalid files
  SET opcode = 201            // Set opcode
SET target = 1            // target type
SET timing = 0            // timing type
SET power = 9            // power
SET resist_dispel = 2    // dispel/resitance
SET duration = 60        // duration
SET probability1 = 100    // probability 1
SET probability2 = 0    // probability 2
SPRINT "resource" "staf11d"         // resource
SET dicenumber = 0        // dice number
SET dicesize = 0        // dice size
SET savingthrow = 0   // saving throw
SET savebonus = 0        // saving throw bonus
SET parameter1 = 100 // param1
SET parameter2 = 1 // param2
SET header = 1
LAUNCH_PATCH_MACRO ADD_SPELL_EFFECT
END
END

// EE quantity fixes

ACTION_FOR_EACH resource IN // Arrows, Bolts, Bullets, and Darts
  ~arow01~
  ~arow02~
  ~arow03~
  ~arow04~
  ~arow05~
  ~arow06~
  ~arow07~
  ~arow08~
  ~arow09~
  ~arow10~
  ~arow11~
  ~arow14~
  ~arow15~
  ~bolt01~
  ~bolt02~
  ~bolt03~
  ~bolt04~
  ~bolt05~
  ~bolt06~
  ~bolt07~
  ~bolt08~
  ~bolt09~
  ~sahbolt~
  ~kuobolt~
  ~kuobolt2~
  ~kuobolt3~
  ~bull01~
  ~bull02~
  ~bull03~
  ~bull04~
  ~bull05~
  ~bull06~
  ~dart01~
  ~dart02~
  ~dart03~
  ~dart04~
  ~dart05~
  ~ohdart50~
  ~ohdart51~
BEGIN
  ACTION_IF (FILE_EXISTS_IN_GAME ~%resource%.itm~) BEGIN
    COPY_EXISTING ~%resource%.itm~ ~override\%resource%.itm~
    PATCH_IF (SOURCE_SIZE > 0x71) BEGIN
      WRITE_BYTE 0x38 0x50
    END
  END
END

ACTION_FOR_EACH resource IN // Throwing Axes and Throwing Daggers
  ~ax1h04~
  ~dagg05~
  ~dagg16~
BEGIN
  ACTION_IF (FILE_EXISTS_IN_GAME ~%resource%.itm~) BEGIN
    COPY_EXISTING ~%resource%.itm~ ~override\%resource%.itm~
    PATCH_IF (SOURCE_SIZE > 0x71) BEGIN
      WRITE_BYTE 0x38 0x28
    END
  END
END

ACTION_FOR_EACH resource IN // Potions
  ~potn02~
  ~potn03~
  ~potn04~
  ~potn05~
  ~potn06~
  ~potn07~
  ~potn08~
  ~potn09~
  ~potn10~
  ~potn11~
  ~potn12~
  ~potn13~
  ~potn14~
  ~potn15~
  ~potn16~
  ~potn17~
  ~potn18~
  ~potn19~
  ~potn20~
  ~potn21~
  ~potn22~
  ~potn23~
  ~potn24~
  ~potn25~
  ~potn26~
  ~potn27~
  ~potn28~
  ~potn29~
  ~potn30~
  ~potn31~
  ~potn32~
  ~potn33~
  ~potn34~
  ~potn35~
  ~potn36~
  ~potn37~
  ~potn38~
  ~potn39~
  ~potn40~
  ~potn41~
  ~potn42~
  ~potn43~
  ~potn44~
  ~potn45~
  ~potn46~
  ~potn52~
  ~potn55~
BEGIN
  ACTION_IF (FILE_EXISTS_IN_GAME ~%resource%.itm~) BEGIN
    COPY_EXISTING ~%resource%.itm~ ~override\%resource%.itm~
    PATCH_IF (SOURCE_SIZE > 0x71) BEGIN
      WRITE_BYTE 0x38 0x18
    END
  END
END

END // EE fixes


/* ---------------- BG1 Fixes ---------------- */

// Item resource fixes
ACTION_IF (GAME_IS ~tutu tutu_totsc bgee~ AND NOT GAME_IS ~bg2 tob bgt bg2ee eet~) BEGIN
  COPY ~item_rev/itm/ax1h03_.itm~ ~override/ax1h03.itm~  // Battle Axe of Mauletar
    SAY NAME1 @1055    SAY NAME2 @2569
    SAY UNIDENTIFIED_DESC @1058 SAY IDENTIFIED_DESC @2570
    LPF update_item_descriptions_to_bgee END
  COPY ~item_rev/itm/belt09.itm~ ~override/belt06.itm~ // Girdle of Trollish Fortitude
    SAY NAME1 @1085    SAY NAME2 @1099
    SAY UNIDENTIFIED_DESC @1087 SAY IDENTIFIED_DESC @1100
    LPF update_item_descriptions_to_bgee END
  COPY ~item_rev/itm/belt09.spl~ ~override~ // Regeneration
    SAY NAME1 @4004
  COPY ~item_rev/itm/blun11_.itm~ ~override/blun11.itm~  // Krotan's Skullcrusher
    SAY NAME1 @1112    SAY NAME2 @2571
    SAY UNIDENTIFIED_DESC @1115 SAY IDENTIFIED_DESC @2572
    LPF update_item_descriptions_to_bgee END
  COPY ~item_rev/itm/leat07_.itm~ ~override/leat07.itm~  // Telbar's Armor
    SAY NAME1 @1636    SAY NAME2 @2573
    SAY UNIDENTIFIED_DESC @1640 SAY IDENTIFIED_DESC @2574
    LPF update_item_descriptions_to_bgee END
  COPY ~item_rev/itm/plat02_.itm~ ~override/plat02.itm~  // Fallorain's Plate
    SAY NAME1 @1825    SAY NAME2 @2575
    SAY UNIDENTIFIED_DESC @1828 SAY IDENTIFIED_DESC @2576
    LPF update_item_descriptions_to_bgee END
  COPY ~item_rev/itm/plat05_.itm~ ~override/plat05.itm~  // Plate of the Dark
    SAY NAME1 @1497    SAY NAME2 @2577
    SAY UNIDENTIFIED_DESC @1499 SAY IDENTIFIED_DESC @2578
    LPF update_item_descriptions_to_bgee END
  COPY ~item_rev/itm/plat06_.itm~ ~override/plat06.itm~  // Ankheg Plate Mail
    SAY NAME1 @1825    SAY NAME2 @2579
    SAY UNIDENTIFIED_DESC @1828 SAY IDENTIFIED_DESC @2580
    LPF update_item_descriptions_to_bgee END
  COPY ~item_rev/itm/bgmisc89.itm~ ~override/misc89.itm~ // Edwin's Amulet
    SAY NAME1 @1780    SAY NAME2 @1780
    SAY UNIDENTIFIED_DESC @1781 SAY IDENTIFIED_DESC @1781
    LPF update_item_descriptions_to_bgee END
  COPY ~item_rev/itm/slng05.itm~ ~override/slng03.itm~   // Arla's Dragonbane
    SAY NAME1 @1920    SAY NAME2 @1929
    SAY UNIDENTIFIED_DESC @1923 SAY IDENTIFIED_DESC @1930
    LPF update_item_descriptions_to_bgee END
  COPY ~item_rev/itm/sw1h09_.itm~ ~override/sw1h09.itm~  // The Whistling Sword
    SAY NAME1 @2066    SAY NAME2 @2581
    SAY UNIDENTIFIED_DESC @2069 SAY IDENTIFIED_DESC @2582
    LPF update_item_descriptions_to_bgee END
  ADD_PROJECTILE ~item_rev/itm/sw1h09.pro~
  COPY ~item_rev/itm/sw1h09.spl~ ~override~
    WRITE_SHORT 0x98 %sw1h09%
  COPY ~item_rev/itm/sw1h09.eff~ ~override~
  COPY ~item_rev/itm/bgsw2h07.itm~ ~override/sw2h07.itm~ // World's Edge
    SAY NAME1 @1760    SAY NAME2 @2426
    SAY UNIDENTIFIED_DESC @1762 SAY IDENTIFIED_DESC @2427
    LPF update_item_descriptions_to_bgee END
END


/* ---------------- BG1EE Fixes ---------------- */

ACTION_IF (GAME_IS ~bgee~ AND NOT GAME_IS ~tutu tutu_totsc bg2 tob bgt bg2ee eet~) BEGIN
  COPY_EXISTING ~blun41.itm~ override
    LPF ALTER_EFFECT INT_VAR match_opcode = 139 match_parameter1 = 1280 parameter1 = 26050 END
  BUT_ONLY
END

/* ---------------- EET Fixes ---------------- */

// Item resource fixes
ACTION_IF (GAME_IS ~eet~ AND NOT GAME_IS ~tutu tutu_totsc bgee bg2 tob bgt bg2ee~) BEGIN
  ACTION_IF (FILE_EXISTS_IN_GAME ~misc89_.itm~) BEGIN
    COPY_EXISTING ~bgmisc89.itm~ ~override/misc89_.itm~
  END
  ACTION_IF (FILE_EXISTS_IN_GAME ~ring08_.itm~) BEGIN
    COPY_EXISTING ~bgring08.itm~ ~override/ring08_.itm~
  END
  ACTION_IF (FILE_EXISTS_IN_GAME ~sw2h07_.itm~) BEGIN
    COPY_EXISTING ~bgsw2h07.itm~ ~override/sw2h07_.itm~
  END
  ACTION_IF (FILE_EXISTS_IN_GAME ~wand12_.itm~) BEGIN
    COPY_EXISTING ~bgwand12.itm~ ~override/wand12_.itm~
  END
END


//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\\\
////\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\

/* ---------------- Non-EE Fixes ---------------- */

// Invalid opcodes fix
ACTION_IF (GAME_IS ~tutu tutu_totsc bg2 tob bgt~ AND NOT GAME_IS ~bgee bg2ee eet~) BEGIN
  COPY_EXISTING_REGEXP GLOB ~^.+\.spl$~ override // remove spellstates
    GET_OFFSET_ARRAY ab_array SPL_V10_HEADERS
    PHP_EACH ab_array AS int => ab_off BEGIN
      GET_OFFSET_ARRAY2 fx_array ab_off ITM_V10_HEAD_EFFECTS
      PHP_EACH fx_array AS int => fx_off BEGIN
        READ_SHORT fx_off fx_type
        PATCH_IF fx_type > 318 BEGIN
          //PATCH_PRINT "%SOURCE_RES%.spl has invalid opcode %fx_type%"
        END
      END
    END
  BUT_ONLY
  COPY_EXISTING_REGEXP GLOB ~^.+\.spl$~ override
    LPF DELETE_SPELL_EFFECT
      INT_VAR opcode_to_delete = 321
    END
    LPF DELETE_SPELL_EFFECT
      INT_VAR opcode_to_delete = 328
    END
    LPF DELETE_SPELL_EFFECT
      INT_VAR opcode_to_delete = 61
    END
  BUT_ONLY
  // PRINT "All invalid opcodes 61, 321, and 328 successfully removed!"
END


/* ---------------- BG1 Fixes ---------------- */

// Item resource fixes
ACTION_IF (GAME_IS ~tutu tutu_totsc~ AND NOT GAME_IS ~bg2 tob bgt bgee bg2ee eet~) BEGIN
  COPY ~item_rev/itm/belt09.itm~ ~override/belt06.itm~ // Girdle of Trollish Fortitude
    SAY NAME1 @1085    SAY NAME2 @1099
    SAY UNIDENTIFIED_DESC @1087 SAY IDENTIFIED_DESC @1100
  COPY ~item_rev/itm/belt09.spl~ ~override~ // Regeneration
    SAY NAME1 @4004
  COPY ~item_rev/itm/bgmisc89.itm~ ~override/misc89.itm~ // Edwin's Amulet
    SAY NAME1 @1780    SAY NAME2 @1780
    SAY UNIDENTIFIED_DESC @1781 SAY IDENTIFIED_DESC @1781
  COPY ~item_rev/itm/slng05.itm~ ~override/slng03.itm~   // Arla's Dragonbane
    SAY NAME1 @1920    SAY NAME2 @1929
    SAY UNIDENTIFIED_DESC @1923 SAY IDENTIFIED_DESC @1930
  COPY ~item_rev/itm/bgsw2h07.itm~ ~override/sw2h07.itm~ // World's Edge
    SAY NAME1 @1760    SAY NAME2 @2426
    SAY UNIDENTIFIED_DESC @1762 SAY IDENTIFIED_DESC @2427
  COPY ~item_rev/itm/leat07_.itm~ ~override/leat07.itm~  // Telbar's Armor
    SAY NAME1 @1636    SAY NAME2 @2573
    SAY UNIDENTIFIED_DESC @1640 SAY IDENTIFIED_DESC @2574
END


//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\\\
////\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\

/* ---------------- All Games Fixes ---------------- */

// BG2 broken areas fix
ACTION_IF (FILE_EXISTS_IN_GAME ~xr1200.are~) BEGIN
  COPY_EXISTING ~xr1200.are~ ~override/xr2400.are~
  COPY_EXISTING ~xr1200.are~ ~override/xr2600.are~
END

// Fixes for other mods
ACTION_IF (FILE_EXISTS_IN_GAME ~X#RINGRO.itm~) BEGIN // BG1 NPC Project Ring of Human Influence fix
  ACTION_IF (FILE_EXISTS_IN_GAME ~ring30.itm~) BEGIN
    COPY_EXISTING ~ring30.itm~ ~override/X#RINGRO.itm~
  END
END

// Remove underdark Demon Lord's immunity to +4 enchanted weapons (but keep immunity to unenchanted and +1 and +2 enchanted weapons)
ACTION_IF (FILE_EXISTS_IN_GAME ~uddemon.cre~) BEGIN
  COPY_EXISTING ~uddemon.cre~ ~override~
    PATCH_IF (SOURCE_SIZE > 0x2d3) BEGIN
      LPF DELETE_CRE_EFFECT INT_VAR opcode_to_delete = 120 END
    END
    BUT_ONLY
END

/* ---------------- 1pp/EE Fixes ---------------- */

DEFINE_ACTION_MACRO ~Locations~ BEGIN
// for weapons
OUTER_SET wgrey = 16
OUTER_SET wteal = 17
OUTER_SET wpink = 18
OUTER_SET wyellow = 19
OUTER_SET wred = 20
OUTER_SET wblue = 21
OUTER_SET wgreen = 22
// for helmets
OUTER_SET hgrey = 48
OUTER_SET hteal = 49
OUTER_SET hpink = 50
OUTER_SET hyellow = 51
OUTER_SET hred = 52
OUTER_SET hblue = 53
OUTER_SET hgreen = 54
// for shields
OUTER_SET sgrey = 32
OUTER_SET steal = 33
OUTER_SET spink = 34
OUTER_SET syellow = 35
OUTER_SET sred = 36
OUTER_SET sblue = 37
OUTER_SET sgreen = 38
// for armour
OUTER_SET agrey = 0
OUTER_SET ateal = 1
OUTER_SET apink = 2
OUTER_SET ayellow = 3
OUTER_SET ared = 4
OUTER_SET ablue = 5
OUTER_SET agreen = 6
END

DEFINE_PATCH_MACRO ~clear~ BEGIN
SET opcode_to_delete = 7
LAUNCH_PATCH_MACRO DELETE_ITEM_EQEFFECT
SET opcode_to_delete = 8
LAUNCH_PATCH_MACRO DELETE_ITEM_EQEFFECT
SET opcode_to_delete = 9
LAUNCH_PATCH_MACRO DELETE_ITEM_EQEFFECT
END

DEFINE_PATCH_MACRO ~colour~ BEGIN
SET opcode = 7      // Set opcode
SET target = 1      // target type
SET timing = 2      // timing type
SET power = 0      // power
SET resist_dispel = 0  // dispel/resitance
SET duration = 0    // duration
SET probability1 = 100  // probability 1
SET probability2 = 0  // probability 2
SPRINT "resource" ""     // resource
SET dicenumber = 0    // dice number
SET dicesize = 0    // dice size
SET savingthrow = 0   // saving throw
SET savebonus = 0    // saving throw bonus
SET parameter1 = gradient // set colour gradient
SET parameter2 = location // set location
LAUNCH_PATCH_MACRO ADD_ITEM_EQEFFECT
END

DEFINE_PATCH_MACRO ~pulse~ BEGIN
SET opcode = 9    // Set opcode
SET target = 1      // target type
SET timing = 2      // timing type
SET power = 0      // power
SET resist_dispel = 0  // dispel/resitance
SET duration = 0    // duration
SET probability1 = 100  // probability 1
SET probability2 = 0  // probability 2
SPRINT "resource" ""     // resource
SET dicenumber = 0    // dice number
SET dicesize = 0    // dice size
SET savingthrow = 0   // saving throw
SET savebonus = 0    // saving throw bonus
SET setrgb = (setb << 24)+(setg << 16)+(setr << 8) // Set RGB
SET parameter1 = setrgb
SET settwo = (speed << 16)+(location) // Set location and speed
SET parameter2 = settwo
LAUNCH_PATCH_MACRO ADD_ITEM_EQEFFECT
END

DEFINE_PATCH_MACRO ~glow~ BEGIN
SET opcode = 8    // Set opcode
SET target = 1      // target type
SET timing = 2      // timing type
SET power = 0      // power
SET resist_dispel = 0  // dispel/resitance
SET duration = 0    // duration
SET probability1 = 100  // probability 1
SET probability2 = 0  // probability 2
SPRINT "resource" ""     // resource
SET dicenumber = 0    // dice number
SET dicesize = 0    // dice size
SET savingthrow = 0   // saving throw
SET savebonus = 0    // saving throw bonus
SET setrgb = (setb << 24)+(setg << 16)+(setr << 8) // Set RGB
SET parameter1 = setrgb
SET parameter2 = location // set location
LAUNCH_PATCH_MACRO ADD_ITEM_EQEFFECT
END

LAUNCH_ACTION_MACRO ~Locations~

// Set color settings
OUTER_SET pcolour = 1
ACTION_IF 1pp_ee_helms_shields_colors = 2 BEGIN
  OUTER_SET pcolour = 2
END
ACTION_IF 1pp_ee_helms_shields_colors = 3 BEGIN
  OUTER_SET pcolour = 4
END

INCLUDE ~item_rev/components/main/fixes/1pp_main.tpa~
INCLUDE ~item_rev/components/main/fixes/1pp_extra.tpa~
INCLUDE ~item_rev/components/main/fixes/1pp_projectiles.tpa~

/* ---------------- Miscellaneous ---------------- */

INCLUDE ~item_rev/components/main/fixes/thrown_hammers.tpa~
INCLUDE ~item_rev/components/main/fixes/stacking.tpa~
INCLUDE ~item_rev/components/main/fixes/item_charges.tpa~
INCLUDE ~item_rev/components/main/fixes/ee_si.tpa~