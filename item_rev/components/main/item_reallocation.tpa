// Reallocate items for balance/gameplay purposes (non-Store Revisions)

/////////////////////////////////////

// Increase the difficulty of stealing Ribald's Ring of Regeneration

ACTION_IF (FILE_EXISTS_IN_GAME ~ribald.cre~) BEGIN
  COPY_EXISTING ribald.cre override LPF FJ_CRE_VALIDITY RET valid=valid END PATCH_IF valid BEGIN
    WRITE_BYTE 0x6a 90
  END BUT_ONLY
END

/////////////////////////////////////

// Belm moved to Ihtafeer

ACTION_IF (FILE_EXISTS_IN_GAME ~trrak01.cre~) BEGIN
  COPY_EXISTING ~trrak01.cre~ ~override~ LPF FJ_CRE_VALIDITY RET valid=valid END PATCH_IF valid BEGIN
    REMOVE_CRE_ITEM sw1h04
    ADD_CRE_ITEM sw1h30 #0 #0 #0 NONE WEAPON1 EQUIP
    LPF ADD_CRE_EFFECT INT_VAR opcode=233 target=0 timing=1 power=0 parameter1=3 parameter2=95 END
  END BUT_ONLY

  COPY_EXISTING ar1905.are override PATCH_IF SOURCE_SIZE>0x11c BEGIN
    LPF DELETE_AREA_ITEM STR_VAR item_to_delete=sw1h30 END
  END BUT_ONLY
END

/////////////////////////////////////

// Remove Girdle of Frost Giant Strength from Death Knight and properly equip him, also give him Helm of Despair
// Give Girdle to Shadow Dragon and remove Scroll of Crom Faeyr
// Place Scroll of Crom Faeyr in Underdark Beholder Lair

ACTION_IF (FILE_EXISTS_IN_GAME ~uddeath2.cre~) BEGIN
  COPY_EXISTING uddeath2.cre override LPF FJ_CRE_VALIDITY RET valid=valid END PATCH_IF valid BEGIN
    REMOVE_CRE_ITEM plat16 sw2h08 belt08
    PATCH_IF comp_ir=1 && comp_cursed=0 BEGIN
      ADD_CRE_ITEM helm02 #0 #0 #0 NONE HELMET
    END
    ADD_CRE_ITEM plat17 #0 #0 #0 NONE ARMOR
    ADD_CRE_ITEM sw2h08 #0 #0 #0 NONE WEAPON1 EQUIP TWOHANDED
    LPF ADD_CRE_EFFECT INT_VAR opcode=233 target=0 timing=1 power=0 parameter1=3 parameter2=93 END
  END BUT_ONLY

  COPY_EXISTING shadra01.cre override LPF FJ_CRE_VALIDITY RET valid=valid END PATCH_IF valid BEGIN
    REMOVE_CRE_ITEM scrlag
    ADD_CRE_ITEM belt08 #0 #0 #0 NONE INV
  END BUT_ONLY

  COPY_EXISTING ar2101.are override PATCH_IF SOURCE_SIZE>0x11c BEGIN
    LPF ADD_AREA_ITEM INT_VAR container_to_add_to=7 STR_VAR item_to_add=scrlag END
  END BUT_ONLY
END

/////////////////////////////////////

// Equip Skin of the Ghoul, as Yarpen has suggested

ACTION_IF (FILE_EXISTS_IN_GAME ~theshal.cre~) BEGIN
  COPY_EXISTING theshal.cre override LPF FJ_CRE_VALIDITY RET valid=valid END PATCH_IF valid BEGIN
    REMOVE_CRE_ITEM leat13
    ADD_CRE_ITEM leat13 #0 #0 #0 NONE ARMOR
  END BUT_ONLY
END

/////////////////////////////////////

// Give Protection from Magic scroll to Lord Argrim (Mage Stronghold)

ACTION_IF (FILE_EXISTS_IN_GAME ~mgket01.cre~) BEGIN
  COPY_EXISTING mgket01.cre override LPF FJ_CRE_VALIDITY RET valid=valid END PATCH_IF valid BEGIN
    ADD_CRE_ITEM scrl07 #1 #0 #0 NONE INV
  END BUT_ONLY
END

/////////////////////////////////////

// Move Flame of the North from Illithid Lair to Tazok

ACTION_IF (FILE_EXISTS_IN_GAME ~ppmind01.cre~) BEGIN
  COPY_EXISTING ppmind01.cre override LPF FJ_CRE_VALIDITY RET valid=valid END PATCH_IF valid BEGIN
    REMOVE_CRE_ITEM sw2h12
  END BUT_ONLY

  COPY_EXISTING tazok.cre override LPF FJ_CRE_VALIDITY RET valid=valid END PATCH_IF valid BEGIN
    REMOVE_CRE_ITEM sw2h11
    ADD_CRE_ITEM sw2h12 #0 #0 #0 NONE WEAPON1 EQUIP TWOHANDED
    LPF ADD_CRE_EFFECT INT_VAR opcode=233 target=0 timing=1 power=0 parameter1=3 parameter2=93 END
  END BUT_ONLY
END

/////////////////////////////////////

// Move Boots of Phasing from Viekang to House Jae'lat Weaponmaster

ACTION_IF (FILE_EXISTS_IN_GAME ~viekang.cre~) BEGIN
  COPY_EXISTING viekang.cre override LPF FJ_CRE_VALIDITY RET valid=valid END PATCH_IF valid BEGIN
    REMOVE_CRE_ITEM boot08
  END BUT_ONLY

  COPY_EXISTING jael03.cre override LPF FJ_CRE_VALIDITY RET valid=valid END PATCH_IF valid BEGIN
    PATCH_IF comp_cursed=0 BEGIN
      ADD_CRE_ITEM boot08 #0 #0 #0 NONE BOOTS          // Boots of Phasing
      LPF ADD_CRE_EFFECT INT_VAR opcode=233 target=0 timing=1 power=0 parameter1=3 parameter2=90 END
    END
  END BUT_ONLY
END

/////////////////////////////////////

// Give and equip Bala's Axe to Mencar the Pebblecrusher; remove extra copy from UB Planar Sphere

ACTION_IF (FILE_EXISTS_IN_GAME ~sevpat01.cre~) BEGIN
  COPY_EXISTING sevpat01.cre override LPF FJ_CRE_VALIDITY RET valid=valid END PATCH_IF valid BEGIN // Equip
    REMOVE_CRE_ITEM hamm02
    ADD_CRE_ITEM ax1h07 #0 #0 #0 NONE WEAPON1 EQUIP
    LPF ADD_CRE_EFFECT INT_VAR opcode=233 target=0 timing=1 power=0 parameter1=2 parameter2=92 END
  END BUT_ONLY
END

ACTION_IF (FILE_EXISTS_IN_GAME ~AR0413.bcs~) BEGIN
  COPY_EXISTING AR0413.bcs override // Remove extra copy from UB
  DECOMPILE_BCS_TO_BAF
    REPLACE_TEXTUALLY CASE_INSENSITIVE ~ActionOverride("Container 1",CreateItem("AX1H07",0,0,0))~ ~ ~
  COMPILE_BAF_TO_BCS
END

/////////////////////////////////////

// Lord Khellon reward change and quest enable

ACTION_IF (FILE_EXISTS_IN_GAME ~trrak01.cre~) BEGIN
  COPY_EXISTING trevil01.dlg override // Reward change
  DECOMPILE_DLG_TO_D
  REPLACE_TEXTUALLY CASE_INSENSITIVE ~GiveGoldForce(1000)
GiveItemCreate("shld25"~
~GiveGoldForce(5375)
GiveItemCreate("shld25"~
  COMPILE_D_TO_DLG

  COPY_EXISTING AR2000.bcs override // Quest enable
  DECOMPILE_BCS_TO_BAF
    REPLACE_TEXTUALLY CASE_INSENSITIVE ~Alignment(Player1,MASK_EVIL)~ ~ ~
  COMPILE_BAF_TO_BCS
END

/////////////////////////////////////

// Give Flail of Submission to Captain Haegan, slaver captain in Slums
// Give him a +1 Medium Shield

ACTION_IF (FILE_EXISTS_IN_GAME ~haegan.cre~) BEGIN
  COPY_EXISTING haegan.cre override LPF FJ_CRE_VALIDITY RET valid=valid END PATCH_IF valid BEGIN
    REMOVE_CRE_ITEM sw2h01
    ADD_CRE_ITEM dvblun01 #0 #0 #0 NONE WEAPON1 EQUIP
    ADD_CRE_ITEM shld04 #0 #0 #0 NONE SHIELD
    LPF ADD_CRE_EFFECT INT_VAR opcode=233 target=0 timing=1 power=0 parameter1=3 parameter2=100 END
  END BUT_ONLY
END

/////////////////////////////////////

// Give Eagle Bow and UB Boots of the West to Valygar elf ranger

ACTION_IF (FILE_EXISTS_IN_GAME ~valran01.cre~) BEGIN
  COPY_EXISTING valran01.cre override LPF FJ_CRE_VALIDITY RET valid=valid END PATCH_IF valid BEGIN
    REMOVE_CRE_ITEM misc8j // Remove duplicate if Unfinished Business is installed
    REMOVE_CRE_ITEM bow03
    REMOVE_CRE_ITEM bow08
    REMOVE_CRE_ITEM bow99
    ADD_CRE_ITEM bow08 #0 #0 #0 NONE WEAPON1 EQUIP TWOHANDED
    ADD_CRE_ITEM misc8j #0 #0 #0 NONE BOOTS
    LPF ADD_CRE_EFFECT INT_VAR opcode=233 target=0 timing=1 power=0 parameter1=3 parameter2=105 END
  END BUT_ONLY
END

/////////////////////////////////////

// Move Bands of Focus from Draconis to Thelynn'ss, Sendai's kensai

ACTION_IF (FILE_EXISTS_IN_GAME ~bazdra02.cre~) BEGIN
  COPY_EXISTING bazdra02.cre override LPF FJ_CRE_VALIDITY RET valid=valid END PATCH_IF valid BEGIN
    REMOVE_CRE_ITEM brac26
  END BUT_ONLY

  COPY_EXISTING senken01.cre override LPF FJ_CRE_VALIDITY RET valid=valid END PATCH_IF valid BEGIN
    ADD_CRE_ITEM brac26 #0 #0 #0 NONE GLOVES
  END BUT_ONLY
END

/////////////////////////////////////

// Move Girdle of Fire Giant Strength from Yaga-Shura Lair to Berenn

ACTION_IF (FILE_EXISTS_IN_GAME ~hgber01.cre~) BEGIN
  COPY_EXISTING ar5201.are override PATCH_IF SOURCE_SIZE>0x11c BEGIN
    LPF DELETE_AREA_ITEM STR_VAR item_to_delete=belt11 END
  END BUT_ONLY

  COPY_EXISTING hgber01.cre override LPF FJ_CRE_VALIDITY RET valid=valid END PATCH_IF valid BEGIN
    ADD_CRE_ITEM belt11 #0 #0 #0 NONE BELT
    LPF ADD_CRE_EFFECT INT_VAR opcode=233 target=0 timing=1 power=0 parameter1=2 parameter2=93 END
  END BUT_ONLY
END

/////////////////////////////////////

// Move Lavender Ioun Stone from Gromnir to Yaga-Shura

ACTION_IF (FILE_EXISTS_IN_GAME ~gromnir.cre~) BEGIN
  COPY_EXISTING gromnir.cre override LPF FJ_CRE_VALIDITY RET valid=valid END PATCH_IF valid BEGIN
    REMOVE_CRE_ITEM helm26
    ADD_CRE_ITEM helmnoan #0 #0 #0 NONE HELMET
  END BUT_ONLY

  COPY_EXISTING yaga01.cre override LPF FJ_CRE_VALIDITY RET valid=valid END PATCH_IF valid BEGIN
    REMOVE_CRE_ITEM helmnoan
    ADD_CRE_ITEM helm26 #0 #0 #0 NONE HELMET
  END BUT_ONLY
END

/////////////////////////////////////

// Move Malakar from Spellhold troll to Samia kensai
// Place Silver Charm on Samia cleric

ACTION_IF (FILE_EXISTS_IN_GAME ~pptroll1.cre~) BEGIN
  COPY_EXISTING pptroll1.cre override LPF FJ_CRE_VALIDITY RET valid=valid END PATCH_IF valid BEGIN
    REMOVE_CRE_ITEM sw1h45
  END BUT_ONLY

  ACTION_IF FILE_EXISTS_IN_GAME pptroll2.cre BEGIN
  COPY_EXISTING pptroll2.cre override LPF FJ_CRE_VALIDITY RET valid=valid END PATCH_IF valid BEGIN
    REMOVE_CRE_ITEM sw1h45
  END BUT_ONLY
  END

  COPY_EXISTING akae.cre override LPF FJ_CRE_VALIDITY RET valid=valid END PATCH_IF valid BEGIN
    REMOVE_CRE_ITEM sw1h43
    ADD_CRE_ITEM sw1h45 #0 #0 #0 NONE WEAPON1 EQUIP
  END BUT_ONLY
END

ACTION_IF (FILE_EXISTS_IN_GAME ~legdor.cre~) BEGIN
  COPY_EXISTING legdor.cre override LPF FJ_CRE_VALIDITY RET valid=valid END PATCH_IF valid BEGIN
    ADD_CRE_ITEM ~amul18~ #0 #0 #0 NONE AMULET
  END
  BUT_ONLY
END

/////////////////////////////////////

// Move Cloak of Nature's Wrath from Merella to Shadow Patrick

ACTION_IF (FILE_EXISTS_IN_GAME ~uhrang01.cre~) BEGIN
  COPY_EXISTING uhrang01.cre override LPF FJ_CRE_VALIDITY RET valid=valid END PATCH_IF valid BEGIN
    REMOVE_CRE_ITEM clck25
  END BUT_ONLY

  COPY_EXISTING rngsha02.cre override LPF FJ_CRE_VALIDITY RET valid=valid END PATCH_IF valid BEGIN
    ADD_CRE_ITEM clck25 #0 #0 #0 NONE CLOAK
    LPF ADD_CRE_EFFECT INT_VAR opcode=233 target=0 timing=1 power=0 parameter1=5 parameter2=99 END
  END BUT_ONLY
END

/////////////////////////////////////

// Move Cloak of the Wolf from dead body to Anath the werewolf

ACTION_IF (FILE_EXISTS_IN_GAME ~shabod01.cre~) BEGIN
  COPY_EXISTING shabod01.cre override LPF FJ_CRE_VALIDITY RET valid=valid END PATCH_IF valid BEGIN
    REMOVE_CRE_ITEM clck04
  END BUT_ONLY

  COPY_EXISTING rngwlf03.cre override LPF FJ_CRE_VALIDITY RET valid=valid END PATCH_IF valid BEGIN
    ADD_CRE_ITEM clck04 #1 #0 #0 NONE CLOAK
  END BUT_ONLY

  COPY_EXISTING rngwlf04.cre override LPF FJ_CRE_VALIDITY RET valid=valid END PATCH_IF valid BEGIN
    ADD_CRE_ITEM clck04 #1 #0 #0 NONE CLOAK
  END BUT_ONLY

  COPY_EXISTING rngwlf05.cre override LPF FJ_CRE_VALIDITY RET valid=valid END PATCH_IF valid BEGIN
    ADD_CRE_ITEM clck04 #1 #0 #0 NONE CLOAK
  END BUT_ONLY
END

/////////////////////////////////////

// Move Shield of the Order from Yaga-Shura to Jamis Tombelthen the general

ACTION_IF (FILE_EXISTS_IN_GAME ~yaga01.cre~) BEGIN
  COPY_EXISTING yaga01.cre override LPF FJ_CRE_VALIDITY RET valid=valid END PATCH_IF valid BEGIN
    REMOVE_CRE_ITEM shld32
  END BUT_ONLY

  COPY_EXISTING amtgen01.cre override LPF FJ_CRE_VALIDITY RET valid=valid END PATCH_IF valid BEGIN
    REMOVE_CRE_ITEM sw1h73 sw1h77
    ADD_CRE_ITEM sw1h77 #0 #0 #0 NONE WEAPON1 EQUIP
    ADD_CRE_ITEM shld32 #0 #0 #0 NONE SHIELD
  END BUT_ONLY
END

/////////////////////////////////////

// Give Life Stealer to Gromnir's assassin - check for UB's Artemis Entereri

ACTION_IF (FILE_EXISTS_IN_GAME ~gromg09.cre~) BEGIN
  ACTION_IF (FILE_EXISTS_IN_GAME ~ubartemi.xxx~) BEGIN
  END ELSE BEGIN
    COPY_EXISTING gromg09.cre override LPF FJ_CRE_VALIDITY RET valid=valid END PATCH_IF valid BEGIN
      REMOVE_CRE_ITEM eler
      ADD_CRE_ITEM dagg20 #0 #0 #0 NONE WEAPON1 EQUIP
      LPF ADD_CRE_EFFECT INT_VAR opcode=233 target=0 timing=1 power=0 parameter1=3 parameter2=96 END
    END BUT_ONLY
  END
END

/////////////////////////////////////

// Equip Shadow Druid Adherent in Druid's Grove with Spear of Unicorn and Protector of the Second
// Place Dalok's Gnasher in Troll's Mound

ACTION_IF (FILE_EXISTS_IN_GAME ~cefgt01.cre~) BEGIN
  COPY_EXISTING ar0420.are override PATCH_IF SOURCE_SIZE>0x11c BEGIN
    LPF DELETE_AREA_ITEM STR_VAR item_to_delete=leat03 END
  END BUT_ONLY

  COPY_EXISTING ar1904.are override PATCH_IF SOURCE_SIZE>0x11c BEGIN
    LPF DELETE_AREA_ITEM STR_VAR item_to_delete=sper07 END
    LPF ADD_AREA_ITEM INT_VAR container_to_add_to=2 STR_VAR item_to_add=blun24 END
  END BUT_ONLY

  COPY_EXISTING cedruid2.cre override LPF FJ_CRE_VALIDITY RET valid=valid END PATCH_IF valid BEGIN
    REMOVE_CRE_ITEM blun24
    ADD_CRE_ITEM blun01 #0 #0 #0 NONE WEAPON1 EQUIP
    LPF ADD_CRE_EFFECT INT_VAR opcode=233 target=0 timing=1 power=0 parameter1=1 parameter2=115 END
  END BUT_ONLY

  COPY_EXISTING cefgt01.cre override LPF FJ_CRE_VALIDITY RET valid=valid END PATCH_IF valid BEGIN
    REMOVE_CRE_ITEM chan04 sw1h20
    ADD_CRE_ITEM sper07 #0 #0 #0 NONE WEAPON1 EQUIP
    ADD_CRE_ITEM leat03 #0 #0 #0 NONE ARMOR
    LPF ADD_CRE_EFFECT INT_VAR opcode=233 target=0 timing=1 power=0 parameter1=2 parameter2=98 END
  END BUT_ONLY
END

/////////////////////////////////////

// Place Full Plate Mail +1 on Prelate Wessalen

ACTION_IF (FILE_EXISTS_IN_GAME ~hprelate.cre~) BEGIN
  COPY_EXISTING hprelate.cre override LPF FJ_CRE_VALIDITY RET valid=valid END PATCH_IF valid BEGIN
    REMOVE_CRE_ITEM npplat
    ADD_CRE_ITEM plat14 #0 #0 #0 NONE ARMOR
    LPF ADD_CRE_EFFECT INT_VAR opcode=233 target=0 timing=1 power=0 parameter1=2 parameter2=93 END
  END BUT_ONLY
END

/////////////////////////////////////

// Move Impaler to Kuo-Toa Prince

ACTION_IF (FILE_EXISTS_IN_GAME ~sahkng02.cre~) BEGIN
  COPY_EXISTING sahkng02.cre override LPF FJ_CRE_VALIDITY RET valid=valid END PATCH_IF valid BEGIN
    REMOVE_CRE_ITEM sper08
    ADD_CRE_ITEM sper05 #0 #0 #0 NONE WEAPON1 EQUIP
    LPF ADD_CRE_EFFECT INT_VAR opcode=233 target=0 timing=1 power=0 parameter1=3 parameter2=98 END
  END BUT_ONLY

  COPY_EXISTING sahpr2.cre override LPF FJ_CRE_VALIDITY RET valid=valid END PATCH_IF valid BEGIN
    REMOVE_CRE_ITEM sper08
    ADD_CRE_ITEM sper05 #0 #0 #0 NONE WEAPON1 EQUIP
    LPF ADD_CRE_EFFECT INT_VAR opcode=233 target=0 timing=1 power=0 parameter1=3 parameter2=98 END
  END BUT_ONLY

  COPY_EXISTING udprince.cre override LPF FJ_CRE_VALIDITY RET valid=valid END PATCH_IF valid BEGIN
    REMOVE_CRE_ITEM sper01 kuosper
    ADD_CRE_ITEM sper08 #0 #0 #0 NONE WEAPON1 EQUIP
    LPF ADD_CRE_EFFECT INT_VAR opcode=233 target=0 timing=1 power=0 parameter1=3 parameter2=98 END
  END BUT_ONLY
END

/////////////////////////////////////

// Move Backbiter from Riti (in Underdark Imprisonment machine) to Baron Ployer: three wizards and Backbiter's insane damage should make it fun

ACTION_IF (FILE_EXISTS_IN_GAME ~udtrap03.cre~) BEGIN
  COPY_EXISTING udtrap03.cre override LPF FJ_CRE_VALIDITY RET valid=valid END PATCH_IF valid BEGIN
    REMOVE_CRE_ITEM sper03
  END BUT_ONLY

  COPY_EXISTING baronp.cre override LPF FJ_CRE_VALIDITY RET valid=valid END PATCH_IF valid BEGIN
    REMOVE_CRE_ITEM sw2h01
    ADD_CRE_ITEM sper03 #0 #0 #0 NONE WEAPON1 EQUIP
    LPF ADD_CRE_EFFECT INT_VAR opcode=233 target=0 timing=1 power=0 parameter1=2 parameter2=98 END
  END BUT_ONLY
END

/////////////////////////////////////

// Remove Boots of the Shifting Sands from Greyhand (Unfinished Business) and have Raevilin Strathi give Boots of the Shifting Sands instead of Albruin

ACTION_IF (FILE_EXISTS_IN_GAME ~greyhand.cre~) BEGIN
  COPY_EXISTING ~greyhand.cre~ ~override~ LPF FJ_CRE_VALIDITY RET valid=valid END PATCH_IF valid BEGIN
      REMOVE_CRE_ITEM boot09
  END BUT_ONLY

  COPY_EXISTING udtrap02.dlg override
  DECOMPILE_DLG_TO_D
    REPLACE_TEXTUALLY CASE_INSENSITIVE ~GiveItemCreate("sw1h34",~ ~GiveItemCreate("boot09",~
  COMPILE_D_TO_DLG
END

/////////////////////////////////////

// Equip Mail of the Dead and Helm of Balduran on Ilyich

ACTION_IF (FILE_EXISTS_IN_GAME ~duecla01.cre~) BEGIN
  COPY_EXISTING duecla01.cre override LPF FJ_CRE_VALIDITY RET valid=valid END PATCH_IF valid BEGIN
    REMOVE_CRE_ITEM leat01
    ADD_CRE_ITEM helm07 #0 #0 #0 NONE HELMET
    ADD_CRE_ITEM chan03 #0 #0 #0 NONE ARMOR
    LPF ADD_CRE_EFFECT INT_VAR opcode=233 target=0 timing=1 power=0 parameter1=3 parameter2=92 END
  END BUT_ONLY

  COPY_EXISTING ar0602.bcs override
  DECOMPILE_BCS_TO_BAF
    REPLACE_EVALUATE ~^\(.+\)$~ BEGIN
      PATCH_IF (~%MATCH1%~ STRING_CONTAINS_REGEXP ~import0[13]~)=0 BEGIN
        SPRINT ~MATCH1~ ~~ // remove lines with "IMPORT01" and "IMPORT03"
      END
    END ~%MATCH1%~
  COMPILE_BAF_TO_BCS
END

/////////////////////////////////////

// Replace Planar Prison Bounty Hunter's Cloak of the Shield with Cloak of Protection +1

ACTION_IF (FILE_EXISTS_IN_GAME ~pbhunt03.cre~) BEGIN
  COPY_EXISTING pbhunt03.cre override LPF FJ_CRE_VALIDITY RET valid=valid END PATCH_IF valid BEGIN
    REPLACE_TEXTUALLY CASE_INSENSITIVE ~clck20~ ~clck01~
  END BUT_ONLY
END

/////////////////////////////////////

// Replace Boots of Stealth with Ring of Night's Embrace in Shadow Thieves Guildhouse
ACTION_IF (GAME_IS ~bg2 tob bgt bg2ee eet~ AND NOT GAME_IS ~tutu tutu_totsc bgee~) BEGIN // BG2
  COPY_EXISTING ~ar0303.are~ ~override~ // Shadow Thieves Guildhouse
    PATCH_IF (SOURCE_SIZE > 0x109) BEGIN
      LAUNCH_PATCH_FUNCTION ~REPLACE_AREA_ITEM~ STR_VAR old_item = ~boot02~ new_item = ~ring21~ END
    END
    BUT_ONLY
END

/////////////////////////////////////

// Remove Horn of Kazgaroth from Deril (lich form) for ARP, put on Lagole Gon
ACTION_IF (FILE_EXISTS_IN_GAME ~cedelich.cre~) BEGIN
  COPY_EXISTING cedelich.cre override LPF FJ_CRE_VALIDITY RET valid=valid END PATCH_IF valid BEGIN
      REMOVE_CRE_ITEM misc73 // Remove duplicate
    END
    BUT_ONLY
END

ACTION_IF (FILE_EXISTS_IN_GAME ~celich.cre~) BEGIN
  COPY_EXISTING celich.cre override LPF FJ_CRE_VALIDITY RET valid=valid END PATCH_IF valid BEGIN
      ADD_CRE_ITEM misc73 #1 #0 #0 NONE INV
    END
    BUT_ONLY
END

/////////////////////////////////////

// Add Web Sack to Pai'Na if UB's Spider Bane quest is not installed
ACTION_IF (FILE_EXISTS_IN_GAME ~u!spbane.itm~) BEGIN
  END ELSE BEGIN
    ACTION_IF (FILE_EXISTS_IN_GAME ~sppain.cre~) BEGIN
    COPY_EXISTING sppain.cre override LPF FJ_CRE_VALIDITY RET valid=valid END PATCH_IF valid BEGIN
      ADD_CRE_ITEM WAND14 #1 #0 #0 NONE INV
    END BUT_ONLY
  END
END

/////////////////////////////////////

// Replaces Cloak of the Good Archmagi with Cloak of Acid Resistance on Errard
ACTION_IF (FILE_EXISTS_IN_GAME ~sarmag01.cre~) BEGIN // Errard
  COPY_EXISTING sarmag01.cre override LPF FJ_CRE_VALIDITY RET valid=valid END PATCH_IF valid BEGIN
      REMOVE_CRE_ITEM ~clck15~
      ADD_CRE_ITEM dvclck01 #0 #0 #0 NONE ARMOR
    END
    BUT_ONLY
END

/////////////////////////////////////

// Replaces Angurvadal +4 with Unholy Reaver on Watcher's Keep Githyanki Captain
ACTION_IF (FILE_EXISTS_IN_GAME ~gorgit.cre~) BEGIN
  COPY_EXISTING gorgit.cre override PATCH_IF SOURCE_SIZE>0x9b BEGIN
    REMOVE_CRE_ITEM ~sw1h60~
    ADD_CRE_ITEM ~ohreaver~ #0 #0 #0 NONE WEAPON1 EQUIP TWOHANDED
    LPF ADD_CRE_EFFECT INT_VAR opcode=233 target=0 timing=1 power=0 parameter1=3 parameter2=93 END
  END
  BUT_ONLY
END

/////////////////////////////////////

// Places Angurvadal +4 on Watcher's Keep Y'tossi
ACTION_IF (FILE_EXISTS_IN_GAME ~gorwom03.cre~) BEGIN
  COPY_EXISTING gorwom03.cre override PATCH_IF SOURCE_SIZE>0x9b BEGIN
    REMOVE_CRE_ITEM ~marili~
    ADD_CRE_ITEM ~sw1h60~ #0 #0 #0 NONE WEAPON1 EQUIP
    LPF ADD_CRE_EFFECT INT_VAR opcode=233 target=0 timing=1 power=0 parameter1=3 parameter2=90 END
  END
  BUT_ONLY
END

/////////////////////////////////////

// Places a droppable Robe of the Neutral Archmagi on Jaheria assassin
ACTION_IF (FILE_EXISTS_IN_GAME ~jade3.cre~) BEGIN // Jeremon
  COPY_EXISTING jade3.cre override LPF FJ_CRE_VALIDITY RET valid=valid END PATCH_IF valid BEGIN
      ADD_CRE_ITEM clck16 #0 #0 #0 NONE ARMOR
    END
    BUT_ONLY
END

/////////////////////////////////////

// Replace "bad" (unique) copies of items with "good" (generic) copies
// AX1H03 (Battle Axe +2) -> AX1H11
// BLUN08 (Flail) -> BLUN02
// DAGG03 (Dagger +2) -> DAGG15
// PLAT05 (Full Plate Mail +1) -> PLAT14
// SHLD19 (Large Shield +2) -> SHLD30
// SW1H09 (Short Sword +2) -> SW1H29

// Replace bad copies of Battle Axe +2
ACTION_FOR_EACH resource IN
  ~sengua04~
  ~ysfire02~
BEGIN
  ACTION_IF (FILE_EXISTS_IN_GAME ~%resource%.cre~) BEGIN
    COPY_EXISTING ~%resource%.cre~ ~override\%resource%.cre~
      REPLACE_TEXTUALLY CASE_INSENSITIVE ~ax1h03~ ~ax1h11~
    BUT_ONLY
  END
END

ACTION_IF (GAME_IS ~bg2 tob bgt bg2ee eet~ AND NOT GAME_IS ~tutu tutu_totsc bgee~) BEGIN // BG2
  COPY_EXISTING ~ar0420.are~ ~override~ // Planer Sphere
    PATCH_IF (SOURCE_SIZE > 0x109) BEGIN
      LAUNCH_PATCH_FUNCTION ~REPLACE_AREA_ITEM~ STR_VAR old_item = ~ax1h03~ new_item = ~ax1h11~ END
    END
    BUT_ONLY
END

ACTION_IF (GAME_IS ~bg2 tob bgt bg2ee eet~ AND NOT GAME_IS ~tutu tutu_totsc bgee~) BEGIN
  ACTION_IF (FILE_EXISTS_IN_GAME ~gretek.cre~) BEGIN // Gretek in BG1
    COPY_EXISTING gretek.cre override LPF FJ_CRE_VALIDITY RET valid=valid END PATCH_IF valid BEGIN
      REMOVE_CRE_ITEM ~ax1h03~
	  REMOVE_CRE_ITEM ~ax1h03_~
      REMOVE_CRE_ITEM ~ax1h11~
      ADD_CRE_ITEM ~ax1h03_~ #0 #0 #0 NONE WEAPON1 EQUIP
    END
    BUT_ONLY
  END
END

// Replace bad copies of Flail
ACTION_FOR_EACH resource IN
  ~bodfgt01~
  ~braiwa5~
  ~helmho~
  ~obshal02~
BEGIN
  ACTION_IF (FILE_EXISTS_IN_GAME ~%resource%.cre~) BEGIN
    COPY_EXISTING ~%resource%.cre~ ~override\%resource%.cre~
      REPLACE_TEXTUALLY CASE_INSENSITIVE ~blun08~ ~blun02~
    BUT_ONLY
  END
END

ACTION_IF (GAME_IS ~bg2 tob bgt bg2ee eet~ AND NOT GAME_IS ~tutu tutu_totsc bgee~) BEGIN // BG2
  ACTION_IF (FILE_EXISTS_IN_GAME ~ar0602.are~) BEGIN
    COPY_EXISTING ~ar0602.are~ ~override~ // Irenicus' Dungeon
      PATCH_IF (SOURCE_SIZE > 0x109) BEGIN
        LAUNCH_PATCH_FUNCTION ~REPLACE_AREA_ITEM~ STR_VAR old_item = ~blun08~ new_item = ~blun02~ END
      END
      BUT_ONLY
    END
END

ACTION_IF (GAME_IS ~bg2 tob bgt bg2ee eet~ AND NOT GAME_IS ~tutu tutu_totsc bgee~) BEGIN // BG2
  ACTION_IF (FILE_EXISTS_IN_GAME ~xr0602.are~) BEGIN
    COPY_EXISTING ~xr0602.are~ ~override~ // Irenicus' Dungeon #2
      PATCH_IF (SOURCE_SIZE > 0x109) BEGIN
        LAUNCH_PATCH_FUNCTION ~REPLACE_AREA_ITEM~ STR_VAR old_item = ~blun08~ new_item = ~blun02~ END
      END
      BUT_ONLY
    END
END

// Replace bad copies of Dagger +2
ACTION_FOR_EACH resource IN
  ~c6arkan~
  ~c6arkan3~
  ~marek~
  ~marek1~
  // ~vaelasa~
BEGIN
  ACTION_IF (FILE_EXISTS_IN_GAME ~%resource%.cre~) BEGIN
    COPY_EXISTING ~%resource%.cre~ ~override\%resource%.cre~
      REPLACE_TEXTUALLY CASE_INSENSITIVE ~dagg03~ ~dagg15~
    BUT_ONLY
  END
END

// Replace bad copies of Full Plate Mail +1
ACTION_FOR_EACH resource IN
  ~bazliz03~
  ~bazliz04~
  ~deathk1~
  ~deathk~
  ~deckwar1~
  ~gromg10~
  ~knight4~
  ~sarev1~
  ~sarevo2~
  ~sengua04~
  ~yaga03~
BEGIN
  ACTION_IF (FILE_EXISTS_IN_GAME ~%resource%.cre~) BEGIN
    COPY_EXISTING ~%resource%.cre~ ~override\%resource%.cre~
      REPLACE_TEXTUALLY CASE_INSENSITIVE ~plat05~ ~plat14~
    BUT_ONLY
  END
END

ACTION_IF (GAME_IS ~bg2 tob bgt bg2ee eet~ AND NOT GAME_IS ~tutu tutu_totsc bgee~) BEGIN
  ACTION_FOR_EACH resource IN
    ~tamoko~
    ~tamoko2~
  BEGIN
    ACTION_IF (FILE_EXISTS_IN_GAME ~%resource%.cre~) BEGIN
      COPY_EXISTING ~%resource%.cre~ ~override\%resource%.cre~
        REMOVE_CRE_ITEM ~plat05~
        REMOVE_CRE_ITEM ~plat05_~
        REMOVE_CRE_ITEM ~plat14~
        ADD_CRE_ITEM ~plat05_~ #0 #0 #0 NONE ARMOR
      BUT_ONLY
    END
  END
END

// Replace bad copies of Large Shield +2
ACTION_FOR_EACH resource IN
  ~chak~
  ~gorgua03~
  ~gorcamb3~
BEGIN
  ACTION_IF (FILE_EXISTS_IN_GAME ~%resource%.cre~) BEGIN
    COPY_EXISTING ~%resource%.cre~ ~override\%resource%.cre~
      REPLACE_TEXTUALLY CASE_INSENSITIVE ~shld19~ ~shld30~
    BUT_ONLY
  END
END

// Replace bad copies of Short Sword +2
ACTION_IF (GAME_IS ~bg2 tob bgt bg2ee eet~ AND NOT GAME_IS ~tutu tutu_totsc bgee~) BEGIN // BG2
  ACTION_IF (FILE_EXISTS_IN_GAME ~ar0303.are~) BEGIN
    COPY_EXISTING ~ar0303.are~ ~override~ // Mae'var's Guildhouse
    PATCH_IF (SOURCE_SIZE > 0x109) BEGIN
      LAUNCH_PATCH_FUNCTION ~REPLACE_AREA_ITEM~ STR_VAR old_item = ~sw1h09~ new_item = ~sw1h29~ END
    END
    BUT_ONLY
  END
END

ACTION_IF (GAME_IS ~bg2 tob bgt bg2ee eet~ AND NOT GAME_IS ~tutu tutu_totsc bgee~) BEGIN // BG2
  ACTION_IF (FILE_EXISTS_IN_GAME ~ar2210.are~) BEGIN
    COPY_EXISTING ~ar2210.are~ ~override~ // ?
    PATCH_IF (SOURCE_SIZE > 0x109) BEGIN
      LAUNCH_PATCH_FUNCTION ~REPLACE_AREA_ITEM~ STR_VAR old_item = ~sw1h09~ new_item = ~sw1h29~ END
    END
    BUT_ONLY
  END
END

ACTION_FOR_EACH resource IN
  ~ammerc07~
  ~ammerc08~
  ~ammerc09~
  ~arkanisg~
  ~c6kach~
  ~calaha~
  ~deckass1~
  ~deckwar1~
  ~gordeck5~
  ~gordeck6~
  ~gordeck7~
  ~gordeck8~
  ~gordeck9~
  ~gordecka~
  ~hgskl02~
  ~hlketta2~
  ~hlketta~
  ~sarcpt01~
  ~sargrd05~
  ~sargrd09~
  ~sarkis02~
  ~sarkis03~
  ~senorc02~
  ~senorc04~
  ~teltief2~
  ~vvarkan~
  ~vvcat~
  ~yaga03~
  ~dw#vider~
  ~dw#vime1~
BEGIN
  ACTION_IF (FILE_EXISTS_IN_GAME ~%resource%.cre~) BEGIN
    COPY_EXISTING ~%resource%.cre~ ~override\%resource%.cre~
      REPLACE_TEXTUALLY CASE_INSENSITIVE ~sw1h09~ ~sw1h29~
    BUT_ONLY
  END
END

ACTION_FOR_EACH resource IN
  ~bernad5~
  ~ribald3~
  ~trmer04a~
  ~type3~
BEGIN
  ACTION_IF (FILE_EXISTS_IN_GAME ~%resource%.sto~) BEGIN
    COPY_EXISTING ~%resource%.sto~ ~override\%resource%.sto~
      REPLACE_TEXTUALLY CASE_INSENSITIVE ~sw1h09~ ~sw1h29~
    BUT_ONLY
  END
END

ACTION_IF (GAME_IS ~bg2 tob bgt bg2ee eet~ AND NOT GAME_IS ~tutu tutu_totsc bgee~) BEGIN
  ACTION_IF (FILE_EXISTS_IN_GAME ~zargal.cre~) BEGIN // Zargal in BG1
    COPY_EXISTING zargal.cre override LPF FJ_CRE_VALIDITY RET valid=valid END PATCH_IF valid BEGIN
      REMOVE_CRE_ITEM ~sw1h09~
      REMOVE_CRE_ITEM ~sw1h09_~
      REMOVE_CRE_ITEM ~sw1h14~
	  REMOVE_CRE_ITEM ~sw1h29~
      ADD_CRE_ITEM ~sw1h09_~ #0 #0 #0 NONE WEAPON1 EQUIP
    END
    BUT_ONLY
  END
END

/////////////////////////////////////

// Various fixes

// Place Telbar's Armor +2 on Sendai in BG1
ACTION_IF (GAME_IS ~bg2 tob bgt bg2ee eet~ AND NOT GAME_IS ~tutu tutu_totsc bgee~) BEGIN
  ACTION_IF (FILE_EXISTS_IN_GAME ~bgsendai.cre~) BEGIN
    COPY_EXISTING bgsendai.cre override LPF FJ_CRE_VALIDITY RET valid=valid END PATCH_IF valid BEGIN
      REMOVE_CRE_ITEM ~leat07~
      REMOVE_CRE_ITEM ~leat07_~
      ADD_CRE_ITEM ~leat07_~ #0 #0 #0 NONE ARMOR
    END
    BUT_ONLY
  END
END

// Place Fallorain's Plate +1 on Genthore in BG1
ACTION_IF (GAME_IS ~bg2 tob bgt bg2ee eet~ AND NOT GAME_IS ~tutu tutu_totsc bgee~) BEGIN
  ACTION_IF (FILE_EXISTS_IN_GAME ~gentho.cre~) BEGIN // Genthore
    COPY_EXISTING gentho.cre override LPF FJ_CRE_VALIDITY RET valid=valid END PATCH_IF valid BEGIN
      REMOVE_CRE_ITEM ~plat02~
      REMOVE_CRE_ITEM ~plat02_~
      ADD_CRE_ITEM ~plat02_~ #0 #0 #0 NONE ARMOR
    END
    BUT_ONLY
  END
END

// Place both Ring of Sorcery and Ring of Wizardry in BG1
ACTION_IF (FILE_EXISTS_IN_GAME ~sunin.cre~) BEGIN
  COPY_EXISTING sunin.cre override PATCH_IF SOURCE_SIZE>0x9b BEGIN
    REMOVE_CRE_ITEM ~ring08~
    REMOVE_CRE_ITEM ~bgring08~
    REMOVE_CRE_ITEM ~ring08_~
    REMOVE_CRE_ITEM ~ohringwi~
    ADD_CRE_ITEM ~ring08~ #0 #0 #0 NONE RING
  END
END

ACTION_IF (GAME_IS ~bg2 tob bgt bg2ee eet~ AND NOT GAME_IS ~tutu tutu_totsc bgee~) BEGIN
  ACTION_IF (FILE_EXISTS_IN_GAME ~ar6800.are~) BEGIN
    COPY_EXISTING ~ar6800.are~ ~override~ // Friendly Arm Inn (BGT)
    PATCH_IF (SOURCE_SIZE > 0x109) BEGIN
      LAUNCH_PATCH_FUNCTION ~REPLACE_AREA_ITEM~ STR_VAR old_item = ~ring08~ new_item = ~bgring08~ END
      LAUNCH_PATCH_FUNCTION ~REPLACE_AREA_ITEM~ STR_VAR old_item = ~ring08_~ new_item = ~bgring08~ END
      LAUNCH_PATCH_FUNCTION ~REPLACE_AREA_ITEM~ STR_VAR old_item = ~ohringwi~ new_item = ~bgring08~ END
    END
    BUT_ONLY
  END
END

ACTION_IF (GAME_IS ~bgee~ AND NOT GAME_IS ~tutu tutu_totsc bg2 tob bgt bg2ee eet~) BEGIN
  ACTION_IF (FILE_EXISTS_IN_GAME ~ar2300.are~) BEGIN
    COPY_EXISTING ~ar2300.are~ ~override~ // Friendly Arm Inn (BGEE)
    PATCH_IF (SOURCE_SIZE > 0x109) BEGIN
      LAUNCH_PATCH_FUNCTION ~REPLACE_AREA_ITEM~ STR_VAR old_item = ~ring08~ new_item = ~bgring08~ END
      LAUNCH_PATCH_FUNCTION ~REPLACE_AREA_ITEM~ STR_VAR old_item = ~ring08_~ new_item = ~bgring08~ END
      LAUNCH_PATCH_FUNCTION ~REPLACE_AREA_ITEM~ STR_VAR old_item = ~ohringwi~ new_item = ~bgring08~ END
    END
    BUT_ONLY
  END
END

ACTION_IF (GAME_IS ~bg2ee eet~ AND NOT GAME_IS ~tutu tutu_totsc bg2 tob bgt bgee~) BEGIN
  ACTION_IF (FILE_EXISTS_IN_GAME ~bg2300.are~) BEGIN
    COPY_EXISTING ~bg2300.are~ ~override~ // Friendly Arm Inn (EET)
    PATCH_IF (SOURCE_SIZE > 0x109) BEGIN
      LAUNCH_PATCH_FUNCTION ~REPLACE_AREA_ITEM~ STR_VAR old_item = ~ring08~ new_item = ~bgring08~ END
      LAUNCH_PATCH_FUNCTION ~REPLACE_AREA_ITEM~ STR_VAR old_item = ~ring08_~ new_item = ~bgring08~ END
      LAUNCH_PATCH_FUNCTION ~REPLACE_AREA_ITEM~ STR_VAR old_item = ~ohringwi~ new_item = ~bgring08~ END
    END
    BUT_ONLY
  END
END

// Place Ankheg Plate Mail +1 in BG1
ACTION_IF (GAME_IS ~bg2 tob bgt bg2ee eet~ AND NOT GAME_IS ~tutu tutu_totsc bgee~) BEGIN
  ACTION_IF (FILE_EXISTS_IN_GAME ~ar3700.are~) BEGIN // Nashkel (BGT)
    COPY_EXISTING ~ar3700.are~ ~override~
    PATCH_IF (SOURCE_SIZE > 0x109) BEGIN
      LAUNCH_PATCH_FUNCTION ~REPLACE_AREA_ITEM~ STR_VAR old_item = ~plat06~ new_item = ~plat06_~ END
    END
    BUT_ONLY
  END
END

ACTION_IF (GAME_IS ~bg2ee eet~ AND NOT GAME_IS ~tutu tutu_totsc bg2 tob bgt bgee~) BEGIN
  ACTION_IF (FILE_EXISTS_IN_GAME ~bg4800.are~) BEGIN // Nashkel (EET)
    COPY_EXISTING ~bg4800.are~ ~override~
    PATCH_IF (SOURCE_SIZE > 0x109) BEGIN
      LAUNCH_PATCH_FUNCTION ~REPLACE_AREA_ITEM~ STR_VAR old_item = ~plat06~ new_item = ~plat06_~ END
    END
    BUT_ONLY
  END
END

ACTION_IF (GAME_IS ~bg2 tob bgt bg2ee eet~ AND NOT GAME_IS ~tutu tutu_totsc bgee~) BEGIN
  ACTION_IF (FILE_EXISTS_IN_GAME ~taerom.dlg~) BEGIN
    COPY_EXISTING taerom.dlg override
      DECOMPILE_DLG_TO_D
        REPLACE_TEXTUALLY CASE_INSENSITIVE ~plat06_~ ~plat06~
        REPLACE_TEXTUALLY CASE_INSENSITIVE ~plat06~ ~plat06_~
      COMPILE_D_TO_DLG
    BUT_ONLY
  END
END

// Elminster fixes
ACTION_FOR_EACH resource IN
  ~amelm01~
  ~merlin~
  ~jatermin~
BEGIN
  ACTION_IF (FILE_EXISTS_IN_GAME ~%resource%.sto~) BEGIN
    COPY_EXISTING ~%resource%.sto~ ~override\%resource%.sto~
      REPLACE_CRE_ITEM ~amul14~ #1 #0 #0 ~unstealable~ ~AMULET~
      REPLACE_CRE_ITEM ~ring07~ #1 #0 #0 ~unstealable~ ~RING~
      REPLACE_CRE_ITEM ~brac03~ #1 #0 #0 ~unstealable~ ~GLOVES~
      REPLACE_CRE_ITEM ~clck02~ #1 #0 #0 ~unstealable~ ~CLOAK~
    BUT_ONLY
  END
END

// Remove extra Boots of Speed from Lothander
ACTION_IF (FILE_EXISTS_IN_GAME ~lothan.cre~) BEGIN // Lothander
  COPY_EXISTING lothan.cre override LPF FJ_CRE_VALIDITY RET valid=valid END PATCH_IF valid BEGIN
    REMOVE_CRE_ITEM boot01
    ADD_CRE_ITEM bootdriz #1 #0 #0 IDENTIFIED BOOTS
  END
  BUT_ONLY
END

// Replace extra Lawgiver in Ulgoth's Beard home with Bastard Sword +1
ACTION_IF (GAME_IS ~bgt~ AND NOT GAME_IS ~tutu tutu_totsc bg2 tob bgee bg2ee eet~) BEGIN // BGT
  ACTION_IF (FILE_EXISTS_IN_GAME ~aru006.are~) BEGIN
    COPY_EXISTING ~aru006.are~ ~override~ // Ulgoth's Beard home
      PATCH_IF (SOURCE_SIZE > 0x109) BEGIN
        LAUNCH_PATCH_FUNCTION ~REPLACE_AREA_ITEM~ STR_VAR old_item = ~sw1h03~ new_item = ~sw1h02~ END
      END
      BUT_ONLY
  END
END

ACTION_IF (GAME_IS ~eet~ AND NOT GAME_IS ~tutu tutu_totsc bg2 tob bgt bgee bg2ee~) BEGIN // EET
  ACTION_IF (FILE_EXISTS_IN_GAME ~sendai_.cre~) BEGIN
    COPY_EXISTING sendai_.cre override LPF FJ_CRE_VALIDITY RET valid=valid END PATCH_IF valid BEGIN
      REMOVE_CRE_ITEM ~leat07~
      REMOVE_CRE_ITEM ~leat07_~
      ADD_CRE_ITEM ~leat07_~ #0 #0 #0 NONE ARMOR
    END
    BUT_ONLY
  END
END

// Replace Spellhold's bad copy of Ray of Enfeeblement with good copy
ACTION_IF (GAME_IS ~bg2 tob bgt bg2ee eet~ AND NOT GAME_IS ~tutu tutu_totsc bgee~) BEGIN // BG2
  ACTION_IF (FILE_EXISTS_IN_GAME ~ar1513.are~) BEGIN
    COPY_EXISTING ~ar1513.are~ ~override~
    PATCH_IF (SOURCE_SIZE > 0x109) BEGIN
      LAUNCH_PATCH_FUNCTION ~REPLACE_AREA_ITEM~ STR_VAR old_item = ~scrlai~ new_item = ~scrl6f~ END
    END
    BUT_ONLY
  END
END

// Make Gorion scroll unstealable
ACTION_IF (FILE_EXISTS_IN_GAME ~gorion.cre~) BEGIN
  COPY_EXISTING gorion.cre override PATCH_IF SOURCE_SIZE>0x9b BEGIN
    REPLACE_CRE_ITEM scrl3z #1 #0 #0 UNSTEALABLE INV
  END
  BUT_ONLY
END

// Yaga cleric shield
ACTION_IF (FILE_EXISTS_IN_GAME ~yaga04.cre~) BEGIN
COPY_EXISTING yaga04.cre override PATCH_IF SOURCE_SIZE>0x9b BEGIN
    REPLACE_TEXTUALLY CASE_INSENSITIVE ~shdl06~ ~shld06~
  END
  BUT_ONLY
END

// Black Reaver random treasure
ACTION_IF (FILE_EXISTS_IN_GAME ~zilfgt01.cre~) BEGIN
COPY_EXISTING zilfgt01.cre override PATCH_IF SOURCE_SIZE>0x9b BEGIN
    REMOVE_CRE_ITEM ~rntre04~
    ADD_CRE_ITEM ~rndtre04~ #1 #0 #0 NONE INV
  END
  BUT_ONLY
END

// Add Two-Handed Sword to Githyanki anti-paladins
ACTION_IF (FILE_EXISTS_IN_GAME ~gith03.cre~) BEGIN
  COPY_EXISTING gith03.cre override LPF FJ_CRE_VALIDITY RET valid=valid END PATCH_IF valid BEGIN
    ADD_CRE_ITEM sw2h01 #0 #0 #0 NONE INV
  END
  BUT_ONLY
END

/////////////////////////////////////

ACTION_IF item_reallocation BEGIN // reallocate items to match with SCS

// Remove Shadow Veil and Robe of Larloch from Deidre
ACTION_IF (FILE_EXISTS_IN_GAME ~wmart2.sto~) BEGIN
  COPY_EXISTING ~wmart2.sto~ ~override~ // Deidre
    PATCH_IF (SOURCE_SIZE > 0x9b) BEGIN
      REMOVE_STORE_ITEM ~wa2helm~ ~wa2robe~
    END BUT_ONLY
END

// Robe of Larloch moved to Azamantes
ACTION_IF (FILE_EXISTS_IN_GAME ~gorlic01.cre~) BEGIN
  COPY_EXISTING ~gorlic01.cre~ ~override~ LPF FJ_CRE_VALIDITY RET valid=valid END PATCH_IF valid BEGIN
    ADD_CRE_ITEM ~wa2robe~ #0 #0 #0 ~NONE~ ~ARMOR~
  END BUT_ONLY
END

// Shadow Veil moved to Gromnir
ACTION_IF (FILE_EXISTS_IN_GAME ~gromnir.cre~) BEGIN
  COPY_EXISTING ~gromnir.cre~ ~override~ LPF FJ_CRE_VALIDITY RET valid=valid END PATCH_IF valid BEGIN
    REMOVE_CRE_ITEM ~helmnoan~
    ADD_CRE_ITEM ~wa2helm~ #1 #0 #0 NONE HELMET
  END BUT_ONLY
END

END