INCLUDE ~item_rev/lib/descriptions.tpa~

//////////////////////////////////////////////
// Descriptions

DEFINE_PATCH_MACRO ~revised_crit_aversion~ BEGIN

  SPRINT t1 @100422
  REPLACE_TEXTUALLY ~%t1%~ ~~ // Remove line 'Protects...'
  
  SPRINT t1 @100423
  REPLACE_TEXTUALLY ~%t1%~ ~~ // Remove line 'Armor Class...'
  
  SPRINT t1 @1004231
  REPLACE_TEXTUALLY ~%t1%~ ~~ // Remove line '...bonus to AC...'
  
  bonus = ac_bonus
  
  PATCH_IF 
        item_description_style == 0
    && !GAME_IS ~bgee bg2ee eet~
    ||  item_description_style == 1
  BEGIN // non EE
  
    PATCH_IF bonus > 0 BEGIN
      PATCH_IF !(ident || crit_hit_prot) BEGIN bonus = 1 END
    END ELSE BEGIN
      SPRINT bonus @100426 // 0 -> none
    END
    catch = 0
    
    PATCH_IF !catch BEGIN
      SPRINT t1 @100114
      SPRINT t2 @100425
      REPLACE_EVALUATE ~%t1%~ BEGIN catch = 1 END ~%t2%~ // Try to catch 'Weight:' line
    END
    
    PATCH_IF !catch BEGIN
      SPRINT t1 @100427
      SPRINT t2 @100428
      REPLACE_EVALUATE ~%t1%~ BEGIN catch = 1 END ~%t2%~ // Try to catch 'STATISTICS:' line
    END
    
    PATCH_IF crit_hit_prot BEGIN
      SPRINT t1 @100114
      SPRINT t2 @100424
      REPLACE_TEXTUALLY ~%t1%~ ~%t2%~ // Add line 'Protects...'
    END
    
  END ELSE BEGIN // EE or Salk
  
    PATCH_IF bonus > 0 BEGIN
      PATCH_IF !(ident || crit_hit_prot) BEGIN bonus = 1 END
      SPRINT t1 @100427
      SPRINT t2 @100425
      REPLACE_TEXTUALLY ~%t1%~ ~%t2%~ // Add line 'Armor Class Bonus:...'
    END
    
    PATCH_IF crit_hit_prot BEGIN
      SPRINT t1 @100427
      SPRINT t2 @100424
      REPLACE_TEXTUALLY ~%t1%~ ~%t2%~ // Add line 'Protects...'
    END
    
  END
  
END

//////////////////////////////////////////////
// Patching

OUTER_SET tobex = 0
OUTER_SPRINT hack ~Configurable Critical Hit Aversion~
ACTION_IF FILE_EXISTS ~tobex_ini/tobexcore.ini~ BEGIN
  COPY ~TobEx_ini/TobExCore.ini~ ~TobEx_ini/TobExCore.ini~
    PATCH_IF FILE_CONTAINS_EVALUATED (~tobex_ini/tobexcore.ini~ ~%hack%~) BEGIN
      tobex = 1
      REPLACE_TEXTUALLY ~%hack%=0~ ~%hack%=1~
    END
  BUT_ONLY
END

ACTION_IF tobex || GAME_IS ~eet bgee bg2ee iwdee~ BEGIN

  STRING_SET 20696 @48 // Remove 'By Helmet' from 'Critical Hit Averted...'
  
  COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ override
  
  PATCH_IF SOURCE_SIZE > 0x71 BEGIN
    READ_SHORT 0x1c item // Category
    READ_ASCII 0x22 anim (2) NULL // Equipped appearance
    
    PATCH_IF item == 7 BEGIN // Headgear
      READ_LONG 0x18 flags // Flags
      crit_hit_prot = !((flags & BIT25) == BIT25) // Critical Hit Aversion enabled (=1)?
      
      PATCH_IF // items to disable Critical Hit Aversion
            (flags & BIT2) == BIT2                     // droppable
//        && !~%SOURCE_RES%~ STRING_EQUAL_CASE ~helm01~  // Standard Helm
        && !~%SOURCE_RES%~ STRING_EQUAL_CASE ~helm04~  // Helm of Defense
        ||  ~%SOURCE_RES%~ STRING_EQUAL_CASE ~gv#helm~ // ?
      BEGIN // Disable Critical Hit Aversion
        WRITE_LONG 0x18 THIS | BIT25
        crit_hit_prot = 0 // Critical Hit Aversion disabled
      END
      
      ac_bonus = 0
      READ_LONG  0x6a fx_off
      READ_SHORT 0x70 fx_num
      FOR (i = 0; i < fx_num; ++i) BEGIN // Search for existing AC bonus
        READ_SHORT (fx_off +        i * 0x30) opcode
        READ_LONG  (fx_off + 0x04 + i * 0x30) param1
        READ_LONG  (fx_off + 0x08 + i * 0x30) param2
        PATCH_IF opcode == 0 && param2 == 0 BEGIN // AC bonus to all weapon?
          i = fx_num // End search after first find
          ac_bonus = param1 // Save AC bonus
        END
      END
      
      PATCH_IF // items to receive replacment AC bonus
            ac_bonus == 0                   // No AC bonus, yet
        && !crit_hit_prot                   // Critical Hit Aversion disabled
        &&  (
           !~%anim%~ STRING_EQUAL_CASE ~~   // No valid Equipped Appearance
        && !~%anim%~ STRING_EQUAL_CASE ~  ~ // No Equipped Appearance
        && !~%anim%~ STRING_EQUAL_CASE ~JB~ // No Circlet
//        ||  ~%SOURCE_RES%~ STRING_EQUAL_CASE ~helm24~ // Obsidian Ioun Stone
            )
      BEGIN // Add AC bonus +1
        ac_bonus = 1
        LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 0 timing = 2 target = 1 parameter1 = ac_bonus END
      END
      
      SPRINT text_update revised_crit_aversion
      LPM update_item_descriptions
      
    END
    
  END
  
  BUT_ONLY
  
END ELSE BEGIN

  FAIL ~This component requires ToBEx installed or an Enhanced Edition game.~
  
END

