INCLUDE ~item_rev/lib/descriptions.tpa~
INCLUDE ~item_rev/lib/usability_description.tpa~

//////////////////////////////////////////////
// Descriptions

DEFINE_PATCH_MACRO wand BEGIN
  PATCH_IF ident BEGIN

    // Make usable by thieves
    SPRINT add_class @100402
    SPRINT catch_class @100404
    LPF usable_desc INT_VAR make_usable=1 STR_VAR add_class catch_class END

    // Add intelligence requirement
    exists=0
    SPRINT t1 @100405
    REPLACE_EVALUATE ~%t1%~ BEGIN
      exists=1
      SPRINT t2 @100406
    END ~%t2%~
    PATCH_IF exists=0 BEGIN
      SPRINT t1 @100400
      SPRINT t2 @100401
      PATCH_IF (~%description%~ STRING_CONTAINS_REGEXP ~\(%t1%\|%t2%\)~) = 0 BEGIN
        SPRINT t3 @100408
        REPLACE_TEXTUALLY ~\(%t1%\|%t2%\)~ ~%t3%~
      END
    END
  END
END

DEFINE_PATCH_MACRO wand_of_heaven BEGIN
  PATCH_IF ident BEGIN
    exists=0
    SPRINT t1 @100405
    REPLACE_EVALUATE ~%t1%~ BEGIN
      exists=1
      SPRINT t2 @100409
    END ~%t2%~
    PATCH_IF exists=0 BEGIN
      SPRINT t1 @100400
      SPRINT t2 @100401
      PATCH_IF (~%description%~ STRING_CONTAINS_REGEXP ~\(%t1%\|%t2%\)~) = 0 BEGIN
        SPRINT t3 @100410
        REPLACE_TEXTUALLY ~\(%t1%\|%t2%\)~ ~%t3%~
      END
    END
  END
END

//////////////////////////////////////////////
// Patching

ACTION_DEFINE_ASSOCIATIVE_ARRAY wand BEGIN
  wand02   => 9  // Wand of Fear
  wand03   => 9  // Wand of Magic Missiles
  wand04   => 9  // Wand of Paralyzation
  wand05   => 9  // Wand of Fire
  wand06   => 9  // Wand of Frost
  wand07   => 9  // Wand of Lightning
  wand08   => 9  // Wand of Sleep
  wand09   => 9  // Wand of Polymorphing
  wand10   => 9  // Wand of Monster Summoning
  wand12   => 10 // Rod of Wonder
  wand13   => 10 // Wand of Cloudkill
  wand14   => 9  // Web Sack
  wand15   => 10 // Rod of the Apprenti
  wand18   => 12 // Wand of Spell Striking
  wand19   => 9  // Wand of Cursing
  wand99   => 9  // Wand of Magic Missile
  ohdwand1 => 11 // Glitterdust
  ohnwand1 => 10 // Whips
  o#llwa   => 10 // From Sellswords
  bgwand12 => 9  // from BGT
  wand12_  => 9  // from EET
END

ACTION_PHP_EACH wand AS ind => res BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME ~%ind%.itm~ BEGIN
  COPY_EXISTING ~%ind%.itm~ override PATCH_IF SOURCE_SIZE>0x71 BEGIN
    WRITE_BYTE 0x2a res
    WRITE_SHORT 0x1f (THIS & `0x4202)
    SPRINT text_update wand
    LPM update_item_descriptions
  END BUT_ONLY END
END

COPY_EXISTING ttwand.itm override
              wand11.itm override
PATCH_IF SOURCE_SIZE>0x71 BEGIN
  WRITE_BYTE 0x2a 9  // INT req
  WRITE_BYTE 0x2e 10 // WIS req
  SPRINT text_update wand_of_heaven
  LPM update_item_descriptions
END